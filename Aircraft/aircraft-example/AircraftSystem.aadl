package AircraftSystem
public
with AircraftSubsystems, FlightGuidanceIMA, SEI;

  system AircraftSystem
    properties
      SEI::WeightLimit => 70000.0 kg;
  end AircraftSystem;
 
 
  system implementation AircraftSystem.SubsystemSpec
    subcomponents
      FGS: system AircraftSubsystems::FlightGuidance;
      HYD: bus AircraftSubsystems::HydraulicPressure;
      LDG: system AircraftSubsystems::LandingGear;
      NAV: system AircraftSubsystems::Navigation;
      FUE: bus AircraftSubsystems::FuelFlow;
      ENG: system AircraftSubsystems::Engines;
      APU: system AircraftSubsystems::AuxiliaryPowerUnit;
      BLD: bus AircraftSubsystems::AirFlow;
      ACO: system AircraftSubsystems::AirConditioning;
      CPT: system AircraftSubsystems::Cockpit;
      ELE: bus AircraftSubsystems::ElectricalPower.V1;
      SIG: bus AircraftSubsystems::SignalFlow;
    connections
      FUEtoENG: bus access FUE -> ENG.FuelSupply;
      FUEtoAPU: bus access FUE -> APU.FuelSupply;
      HYDtoENG: bus access HYD -> ENG.HydraulicPower;
      FGStoHYD: bus access HYD -> FGS.HydraulicPower;
      HYDtoLDG: bus access HYD -> LDG.HydraulicPower;
      BLDtoACO: bus access BLD -> ACO.AirPower;
      BLDtoENG: bus access BLD -> ENG.AirPower;
      ELEtoENG: bus access ELE -> ENG.ElectricalPower;
      ELEtoCPT: bus access ELE -> CPT.ElectricalSupply;
      ELEtoNAV: bus access ELE -> NAV.ElectricalSupply;
      ELEtoLDG: bus access ELE -> LDG.ElectricalSupply;
      ELEtoAPU: bus access ELE -> APU.ElectricalSupply;
      ELEtoACO: bus access ELE -> ACO.ElectricalSupply;
      ELEtoEGS: bus access ELE -> FGS.ElectricalSupply;
      ACOtoSIG: bus access SIG -> ACO.Signals;
      APUtoSIG: bus access SIG -> APU.Signals;
      LDGtoSIG: bus access SIG -> LDG.Signals;
      NAVtoSIG: bus access SIG -> NAV.Signals;
      FGStoSIG: bus access SIG -> FGS.Signals;
      ENGtoSIG: bus access SIG -> ENG.Signals;
      CPTtoSIG: bus access SIG -> CPT.Signals;
  end AircraftSystem.SubsystemSpec;
  
  system implementation AircraftSystem.SubsystemImpl
    extends AircraftSystem.SubsystemSpec
    subcomponents
      FGS: refined to system FlightGuidanceIMA::FlightGuidance.cmdflow;
  end AircraftSystem.SubsystemImpl;
end AircraftSystem;
